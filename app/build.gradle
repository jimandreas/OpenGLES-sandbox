apply plugin: 'com.android.application'

// versioning from stackoverflow and Jake Wharton
// https://plus.google.com/+JakeWharton/posts/6f5TcVPRZij
// http://stackoverflow.com/questions/4616095/how-to-get-the-build-version-number-of-your-android-application

def versionMajor = 1
def versionMinor = 0
def versionPatch = 8
def versionBuild = 8 // bump for dogfood builds, public betas, etc.

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.learnopengles.sandbox"
        minSdkVersion 16
        targetSdkVersion 27

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
}

// signing solution courtesy of http://gist.github.com/gabrielemariotti/6856974

def Properties props = new Properties()
def propFile = new File('gradle/signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'gradle/signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'gradle/signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation 'com.jakewharton.timber:timber:4.6.1'
    testImplementation 'junit:junit:4.12'

    // constraint layout - latest and greatest Android Studio helper
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta5'

    // FAB button
//    implementation 'com.android.support:design:22.2.1'
    implementation 'com.android.support:design:27.1.0'
}
